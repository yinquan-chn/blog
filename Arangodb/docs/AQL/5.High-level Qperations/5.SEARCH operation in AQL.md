[SEARCH operation in AQL](https://docs.arangodb.com/3.11/aql/high-level-operations/search/)

# SHEARCH操作
SEARCH操作筛选视图，底层索引加速查询速度

SEARCH操作保证将视图索引用于高效的执行计划。如果对“视图”使用FILTER关键字，则不会使用索引，并且过滤是作为后处理步骤执行的。

从概念上讲，视图只是另一个文档数据源，类似于数组或文档/边缘集合，您可以使用AQL中的FOR操作对其进行迭代：
```
FOR doc IN viewName
  RETURN doc
```

可选的SEARCH操作提供了以下功能：

- 基于AQL布尔表达式和函数过滤文档
- 利用快速索引匹配不同集合中的文档
- 根据文档与搜索条件的匹配程度对结果集进行排序

有关如何设置视图，请参阅[arangsearch Views](https://docs.arangodb.com/3.11/index-and-search/arangosearch/arangosearch-views-reference/)和[search alias Views](https://docs.arangodb.com/3.11/index-and-search/arangosearch/search-alias-views-reference/)

## 语法

SEARCH 关键字后面跟随的是 ArangoSearch 过滤表达式，该表达式主要由对 ArangoSearch AQL 函数的调用组成。
```
FOR doc IN viewName
  SEARCH expression
  OPTIONS { … }
  ...
```

## 使用

SEARCH 语句与 FILTER 语句不同，它被视为 FOR 操作的一部分，而非独立的语句。因此，SEARCH 不能自由地放置在查询中的任何位置，也不能在同一 FOR 循环体内出现多次。FOR ... IN 后面必须跟一个视图（View）的名字，而不是集合（collection）。紧接着 FOR ... IN 的必须是 SEARCH 操作，不允许在此位置之前放置诸如 FILTER、COLLECT 等操作。然而，在 SEARCH 之后可以执行其他操作，比如使用 SORT 对搜索结果按由视图计算出的排名值进行排序。

表达式必须是一个 ArangoSearch 表达式。ArangoSearch 的全部功能通过特殊的 ArangoSearch 函数在搜索和排序阶段得以利用和展现。此外，还支持常见的 AQL 运算符。

需要注意的是，内联表达式以及其他一些特性并不被 SEARCH 支持。如果表达式无效，服务器会抛出查询错误。

在搜索表达式之后，可以选择性地跟随 OPTIONS 关键字以及一个对象来设置搜索选项。

## 逻辑操作
逻辑运算符或布尔运算符允许组合多个搜索条件。

- AND, && (与)
- OR, || (或)
- NOT, ! (非)

需要考虑运算符优先级，并且可以使用括号进行控制。

思考以下表达式：
```
doc.value < 0 OR doc.value > 5 AND doc.value IN [-10, 10]
```
AND的优先级高于OR。该表达式等效于：
```
doc.value < 0 OR (doc.value > 5 AND doc.value IN [-10, 10])
```
此条件表达是:


- value 小于0
- 值大于5，但仅当它是10（或-10，但这永远无法实现）

括号可按如下方式用于将AND条件应用于两个OR条件：
```
(doc.value < 0 OR doc.value > 5) AND doc.value IN [-10, 10]
```
此条件表达是:


- 值小于0，但仅当它为-10时
- 值大于5，但仅当它为10时

## 比较运算符
- == (equal)
- <= (less than or equal)
- >= (greater than or equal)
- < (less than)
- > (greater than)
- != (unequal)
- IN (contained in array or range), also NOT IN
- LIKE (equal with wildcards), also NOT LIKE

另请参阅[IN_RANGE（）函数](https://docs.arangodb.com/3.11/aql/functions/arangosearch/#in_range)，以获取用于范围搜索的<、<=、>、>=运算符组合的替代方法。
```
FOR doc IN viewName
  SEARCH ANALYZER(doc.text == "quick" OR doc.text == "brown", "text_en")
  // -- or --
  SEARCH ANALYZER(doc.text IN ["quick", "brown"], "text_en")
  RETURN doc
```
ArangoSearch不考虑字符的字母顺序，即针对视图的SEARCH操作中的范围查询将不遵循定义的Analyzer区域设置（排序规则Analyzer除外）或服务器语言（启动选项-默认语言）的语言规则！另请参阅已知问题。

## 数组比较运算符
数组比较运算符支持：
```
LET tokens = TOKENS("some input", "text_en")                 // ["some", "input"]
FOR doc IN myView SEARCH tokens  ALL IN doc.text RETURN doc // dynamic conjunction
FOR doc IN myView SEARCH tokens  ANY IN doc.text RETURN doc // dynamic disjunction
FOR doc IN myView SEARCH tokens NONE IN doc.text RETURN doc // dynamic negation
FOR doc IN myView SEARCH tokens  ALL >  doc.text RETURN doc // dynamic conjunction with comparison
FOR doc IN myView SEARCH tokens  ANY <= doc.text RETURN doc // dynamic disjunction with comparison
FOR doc IN myView SEARCH tokens NONE <  doc.text RETURN doc // dynamic negation with comparison
FOR doc IN myView SEARCH tokens AT LEAST (1+1) IN doc.text RETURN doc // dynamically test for a subset of elements
```

以下运算符在SEARCH表达式中等效：

- ALL IN, ALL ==, NONE !=, NONE NOT IN
- ANY IN, ANY ==
- NONE IN, NONE ==, ALL !=, ALL NOT IN
- ALL >, NONE <=
- ALL >=, NONE <
- ALL <, NONE >=
- ALL <=, NONE >
- AT LEAST (...) IN, AT LEAST (...) ==
- AT LEAST (1) IN, ANY IN

操作符右侧引用的存储属性即使是一个数组，在处理时也会像多个独立的基本值那样对待。例如，在全文搜索或标记匹配场景下，即使文档属性实际存储的是一个数组，SEARCH表达式中的IN和==运算符也能分别对数组内的每个元素进行比较，并且可以像处理单个基本值一样自然地应用这些条件。而在SEARCH表达式之外，当使用IN运算符时，它后面通常需要跟一个明确的数组值来进行判断

## 问号操作

在ArangoDB企业版（Enterprise Edition）中，你可以使用问号（?）操作符进行嵌套搜索:
```
FOR doc IN myView
  SEARCH doc.dimensions[? FILTER CURRENT.type == "height" AND CURRENT.value > 40]
  RETURN doc
```
在ArangoDB企业版中，通过配置arangosearch视图或倒排索引（用于搜索别名视图），可以利用特定的语法来处理嵌套数组中的对象，使其满足多个条件，并且可以选择性地指定这些条件在整个数组中应被满足的次数。问号操作符通常在这种上下文中与SEARCH语句结合使用，以实现对嵌套数据结构进行复杂查询的功能。

## 非索引字段的处理
在使用SEARCH操作对视图进行查询时，如果文档属性没有被配置为由该视图索引，则SEARCH会将这些属性视为不存在。

For example, given a collection myCol with the following documents:
例如，给myCol文档的集合：
```
{ "someAttr": "One", "anotherAttr": "One" }
{ "someAttr": "Two", "anotherAttr": "Two" }
```
…构建arangsearch视图，其中someAttr是视图myView索引：
```
{
  "type": "arangosearch",
  "links": {
    "myCol": {
      "fields": {
        "someAttr": {}
      }
    }
  }
}
```
…在someAttr上搜索会产生以下结果：
```
FOR doc IN myView
  SEARCH doc.someAttr == "One"
  RETURN doc
```
```
[ { "someAttr": "One", "anotherAttr": "One" } ]
```
在另一个Attr上搜索会产生一个空结果，因为只有someAttr被视图索引：
```
FOR doc IN myView
  SEARCH doc.anotherAttr == "One"
  RETURN doc
```
```
[]
```
可以使用arangosearch视图的特殊属性includeAllFields,对源文档的所有（子）属性建立索引

## SEARCH 排序
在使用ArangoSearch进行查询时，可以利用标准的SORT()操作对从视图中返回的文档按照属性值进行排序。这可以通过一个或多个属性实现，支持升序或降序（或者混合排序）
```
FOR doc IN viewName
  SORT doc.text, doc.value DESC
  RETURN doc
```
如果排序操作中（最左侧）的字段及其排序方向与视图的主排序顺序定义相匹配，则SORT

除了简单的属性值排序外，还可以根据相关性评分（或按需要组合评分和属性值）对匹配的视图文档进行排序。通过SEARCH关键字进行的文档搜索与ArangoSearch评分函数（如BM25()和TFIDF()）紧密相关联。在SEARCH表达式中给出的查询不仅用于过滤文档，还与评分函数一起使用以决定哪个文档最符合查询条件。视图中的其他文档也会影响这一决策。

因此，ArangoSearch评分函数只能应用于从视图中检索出的文档上，因为排序结果时既需要考虑与之对应的SEARCH表达式，也要参考视图本身的相关配置和内容。
```
FOR doc IN viewName
  SEARCH ...
  SORT BM25(doc) DESC
  RETURN doc
```
BOOST()函数可以用来微调搜索结果的排名，通过对SEARCH表达式中的子表达式赋予不同的权重。

如果在调用评分函数之前没有进行SEARCH操作，或者SEARCH表达式并未过滤任何文档（例如：SEARCH true，表示所有文档都满足条件），那么所有文档将返回一个得分为0的结果。

## Search操作
SEARCH 操作接受一个选项对象，包含以下属性：

- collections（数组，可选）：一个包含字符串的数组，这些字符串代表要限制搜索范围的源集合名称，即只在这些集合中进行搜索。
- conditionOptimization（字符串，可选）：控制搜索条件如何进行优化。可能的值有：

  - "auto"（默认）：将搜索条件转换为合取范式（Conjunctive Normal Form, CNF）并应用优化措施。移除冗余或重叠的条件，但这可能会花费较长时间，即使是少量嵌套条件也是如此。
  - "none"：在不优化条件的情况下搜索索引。
- countApproximate（字符串，可选）：控制在查询启用fullCount选项或执行带有COUNT子句的COLLECT时，如何计算总行数。
  - "exact"（默认）：实际枚举所有行以获得精确计数。
  - "cost"：使用成本估算方式进行近似计算。不枚举行，而是以O(1)的时间复杂度返回近似结果。如果SEARCH条件为空，或者仅包含一个术语查询（例如：SEARCH doc.field == "value"），忽略视图通常的一致性延迟问题，此时能提供精确结果。

示例：
假设有一个视图，该视图链接了三个集合 coll1、coll2 和 coll3，我们可以通过 collections 选项仅返回前两个集合中的文档，并忽略第三个集合：
```
FOR doc IN viewName
  SEARCH true OPTIONS { collections: ["coll1", "coll2"] }
  RETURN doc
```

在这个例子中，搜索表达式 true 匹配视图中的所有文档。您可以在保持搜索范围限定在所选源集合的前提下，使用任何有效的表达式替换 true。这样，查询将仅返回来自 coll1 和 coll2 集合中的文档，而不涉及 coll3 中的数据。